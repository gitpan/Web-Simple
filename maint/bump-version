#!/usr/bin/env perl

use 5.010;
use strict;
use warnings FATAL => 'all';
use autodie;

chomp(my $LATEST = qx(grep '^[0-9]' Changes | head -1 | awk '{print \$1}'));

my @parts = split /\./, $LATEST;

my $OLD_DECIMAL = sprintf('%i.%03i' . (@parts > 2 ? '%03i' : ''), @parts);

my %bump_part = (major => 0, minor => 1, bugfix => $#parts);

my $bump_this = 
  $bump_part{$ARGV[0]||'bugfix'}
    // die "no idea which part to bump - $ARGV[0] means nothing to me";

my @new_parts = @parts;

$new_parts[$bump_this]++;

my $NEW_DECIMAL = sprintf('%i.%03i' . (@parts > 2 ? '%03i' : ''), @new_parts);

my @PM_FILES = ( 'lib/Web/Simple.pm', 'lib/Web/Simple/Role.pm' );

foreach my $filename (@PM_FILES) {
  warn "Bumping $OLD_DECIMAL -> $NEW_DECIMAL in $filename\n";

  my $file = do { local (@ARGV, $/) = ($filename); <> };

  $file =~ s/(?<=\$VERSION = ')${\quotemeta $OLD_DECIMAL}/${NEW_DECIMAL}/;

  open my $out, '>', $filename;

  print $out $file;
}
